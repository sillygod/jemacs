#+TITLE: jemacs-note-taking.el
#+PROPERTY: header-args:emacs-lisp :tangle ./jemacs-note-taking.el :mkdirp yes

* setup

  #+begin_src emacs-lisp
    ;;; mycraft --- Summary  -*- lexical-binding: t; -*-
    ;;; Copyright (C) 2020 mycraft maintainers
    ;;; Author: Jing
    ;;; package --- mycraft
    ;;; Commentary:

    ;;; Code:


  #+end_src

* org

  https://orgmode.org/worg/org-contrib/org-drill.html

  we can check the org's version by the command =org-version=

  #+begin_src emacs-lisp
    (use-package org-ql
      :defer org
      :config
      (setq org-agenda-inhibit-startup t)
      (setq org-agenda-dim-blocked-tasks nil)
      (setq org-agenda-ignore-properties '(effort appt stats category))
      (setq org-agenda-use-tag-inheritance nil))

    (defun my-todo-agenda ()
      "need to research the org-ql"
      (interactive)
      (org-ql-search
        ;; "~/Dropbox/myorgs/life_books_courses_programming/todo.org"
        (directory-files org-roam-directory nil ".org")
        '(todo)
        :title "Things to Be Done"
        :sort '(deadline)
        :super-groups `((:name "Daily plan"
                               :deadline today)
                        (:name "Weekly plan"
                               :and
                               (:deadline (after ,(format-time-string "%Y-%m-%d" (time-subtract (current-time) (* 7 24 60 60))))
                                          :deadline (before ,(format-time-string "%Y-%m-%d" (time-add (current-time) (* 8 24 60 60))))))
                        (:name "Monthly plan"
                               :and
                               (:deadline (after ,(format-time-string "%Y-%m-%d" (time-add (current-time) (* 7 24 60 60))))
                                          :deadline (before ,(format-time-string "%Y-%m-%d" (time-add (current-time) (* 31 24 60 60))))))

                        (:auto-parent t))))

    (use-package org-drill
      :defer t)

    (use-package org
      :defer t
      :init

      (setq-default safe-local-variable-values
                    '((org-reveal-ignore-speaker-notes . (lambda (x) t))
                      (org-confirm-babel-evaluate . (lambda (x) t))
                      (eval . (progn
                                (when
                                    (and (not (equal buffer-file-name nil))
                                         (equal
                                          (string-match
                                           (regexp-quote org-roam-directory)
                                           (regexp-quote buffer-file-name))
                                          nil))
                                  (setq org-roam-directory (locate-dominating-file default-directory ".dir-locals.el"))
                                  (setq org-roam-db-location (concat org-roam-directory "org-roam.db")))))
                      (org-export-babel-evaluate . (lambda (x) t))))
      :custom
      ;; cool! some functions need to be enable
      ;; like <s press tab to complete org structure (org-tempo)
      ;; these variables can be found in the source code of org.el
      (org-pretty-entities t) ;; org-toggle-pretty-entities
      (org-use-sub-superscripts '{})
      ;; make _, ^ only inside the {} will work as subscript or as superscript
      ;; ex. n{^2}
      (org-link-descriptive t)
      (org-modules '(ol-w3m
                     ol-bbdb
                     ol-bibtex
                     ol-docview
                     ol-gnus
                     ol-info
                     ol-irc
                     ol-mhe
                     ol-rmail
                     ol-eww
                     org-habit
                     org-protocol
                     org-tempo))


      ;; to produce font-face for org quote block
      (org-fontify-quote-and-verse-blocks t)
      ;; hide the empasis syntax ex. *hi* -> bold hi
      (org-hide-emphasis-markers t)
      ;; use indent-line-function to
      ;; see the indentation function used by org-mode
      ;; check the doc of org-indent-line, know how indent works
      (org-adapt-indentation t)
      ;; this will make org-shift to auto add timestamp after making a toto item complete
      (org-log-done 'time)
      (org-startup-truncated nil)
      (org-export-backends '(ascii html icalendar latex odt md))

      (org-id-locations-file (expand-file-name "org.org-id-locations" user-emacs-directory))

      (org-image-actual-width nil)
      ;; org-edit-src without prompting window
      (org-src-window-setup 'current-window)
      (org-startup-folded t)
      (org-startup-with-inline-images t)

      (org-agenda-use-tag-inheritance nil)
      (org-hide-leading-stars t)

      :config
      (require 'org-indent)
      (keymap-unset org-mode-map "C-'" t)
      ;; (setq org-ellipsis " ▾")
      (setq-default org-default-notes-file
                    "~/Dropbox/myorgs/todo.org")


      :ensure org-contrib)

    (use-package org-tree-slide
      :defer t
      :custom
      (org-image-actual-iwth nil))

    (use-package org-download
      :commands
      (org-download-screenshot
       org-download-clipboard)
      :custom
      (org-download-screenshot-method "screencapture -i %s")
      (org-download-image-org-width 500)
      (org-download-image-html-width 500)
      (org-download-image-dir "./img")
      :defer t)

    (use-package ox-reveal
      :after org)

    (use-package org-superstar
      :hook (org-mode . org-superstar-mode))

    (defun presentation-setup ()
      ;; Scale the text.  The next line is for basic scaling:
      (with-eval-after-load 'face-remap
        (setq text-scale-mode-amount 3)
        (text-scale-mode 1)))

    (defun presentation-end ()
      ;; Show the mode line again
      (setq text-scale-mode-amount 0)
      (text-scale-mode 0))


    (use-package org-tree-slide
      :defer t
      :hook ((org-tree-slide-play . presentation-setup)
             (org-tree-slide-stop . presentation-end))
      :custom
      (org-tree-slide-slide-in-effect t)
      (org-tree-slide-header t)
      (org-tree-slide-breadcrumbs " > ")
      (org-image-actual-iwth nil))

    (use-package visual-fill-column
      :init
      (setq visual-fill-column-width 150)
      (setq visual-fill-column-center-text t)
      :hook (org-mode . org-mode-visual-fill))

  #+end_src

* org beautify

  #+begin_src emacs-lisp
    (defun get-youtube-video-id (url)
      "Get the 'v' query parameter value from a YouTube video URL."
      (let* ((url-parts (url-generic-parse-url url))
             (query-string (url-filename url-parts))
             (query-params (url-parse-query-string query-string))
             (v-value (cdr (assoc "/watch?v" query-params))))
        (car v-value)))

    (defun insert-xwidget-for-youtube (url)
      "Insert a xwidget in buffer and display the content of url."
      (let ((embed-url (format "https://www.youtube.com/embed/%s?controls=1" (get-youtube-video-id url)))
            (widget (make-xwidget
                     'webkit
                     "Video"
                     (truncate (* (window-pixel-width) 0.8))
                     (truncate (* (window-pixel-width) 0.8 0.75)))))
        (insert
         (propertize
          " [video] "
          'display (list 'xwidget :xwidget widget)))
        (xwidget-webkit-execute-script
         widget (format "document.body.innerHTML = %S;"
                        (format
                         "<style>body { margin: 0px; }</style><div style='background: black; height: 100%%; display: flex; align-items: center; justify-content: center;'><iframe width='%s' height='%s' src=%s></iframe></div>"
                         "100%" "100%" embed-url)))))

  #+end_src

* writeroom mode
  #+begin_src emacs-lisp
    (use-package writeroom-mode
      :custom
      (writeroom-width 220)
      :defer t)
  #+end_src

* htmlize

  it's required when you want the html exported by =org-export= with syntax highlight effect.

  #+begin_src emacs-lisp
    (use-package htmlize
      :defer t)
  #+end_src

* org-roam

  This requires =sqlite3=. Ensure it's installed.

  Remember to run =org-roam-db-build-cache= at first time.

  #+begin_src emacs-lisp

    (use-package org-roam
      :straight
      (:host github :repo "org-roam/org-roam" :files (:defaults "extensions/*"))
      :after org
      :custom
      (org-roam-directory "/Users/jing/Dropbox/myorgs/life_books_courses_programming/")
      (org-roam-db-location (concat org-roam-directory "org-roam.db"))
      (org-roam-completion-everywhere t)
      :init
      (setq org-roam-v2-ack t)

      :config
      (setq org-roam-node-display-template
            (concat "${title:100}" (propertize "${tags:40}" 'face 'org-tag)))
      (setq org-roam-dailies-capture-templates
            `(("d" "default" entry
               "* %<%H:%M> %?"
               :target (file+head "%<%Y-%m-%d>.org"
                                  "#+title: %<%Y-%m-%d>\n#+filetags: :daily:"))))
      (setq org-roam-capture-templates
            `(("d" "default" plain "%?" :target
               (file+head "${slug}.org" "#+title: ${title}\n")
               :unnarrowed t)
              ("b" "book" plain
               "* The book in 3 sentences or 3 points\n* Impresisions\n* How the Book Changed Me\n* Favorite Quotes\n* Summary + Notes for each sections\n "
               :target
               (file+head "${slug}.org" "#+title: ${title}\n#+filetags: :book:\n\nauthor:"))
              ("c" "course" plain
               "* Impressions\n* Summary + Notes for each section\n "
               :target
               (file+head "${slug}.org" "#+title: ${title}\n#+filetags: :course:\n\nlink:\n"))))
      (setq org-roam-dailies-directory "journal/")
      (org-roam-db-autosync-enable))


    (use-package org-roam-ui
      :straight
        (:host github :repo "org-roam/org-roam-ui" :branch "main" :files ("*.el" "out"))
        :after org-roam
        :config
        (setq org-roam-ui-sync-theme t
              org-roam-ui-follow t
              org-roam-ui-update-on-save t
              org-roam-ui-open-on-start nil))


    (defun ora-org-roam-find-file-action (x)
      (if (consp x)
          (let ((file-path (plist-get (cdr x) :path)))
            (org-roam--find-file file-path))
        (let* ((title-with-tags x)
               (org-roam-capture--info
                `((title . ,title-with-tags)
                  (slug . ,(funcall org-roam-title-to-slug-function title-with-tags))))
               (org-roam-capture--context 'title))
          (setq org-roam-capture-additional-template-props (list :finalize 'find-file))
          (org-roam-capture--capture))))

  #+end_src

* org-transclusion

  # TODO: setup transclusion usages
  # customize the fringe color and background of transclusion block
  # research why it will try to read all org roam file when referring to a org roam file
  # https://nobiot.github.io/org-transclusion/#Faces-_0026-fringe-bitmap
  #+transclude: [[id:CD451D64-4615-4339-8058-128E33653FC3][Find Job Fight]] :lines 5-20

  #+begin_src emacs-lisp
    (use-package org-transclusion
      :after org)
  #+end_src

* ob-async
  #+begin_src emacs-lisp
    (use-package ob-async
      :defer t)
  #+end_src

* toc-org
  #+begin_src emacs-lisp
    (use-package toc-org
      :defer t
      :hook
      (org-mode . toc-org-mode)
      (markdown-mode . toc-org-mode)
      :commands (toc-org-insert-toc))
  #+end_src

* evil-org

  By default, you need to press M-RET to add a auto-numbering list
  this will has some agenda mode binding..

  If you want the key binding for org-agenda-mode, add the following settings.

  #+begin_src emacs-lisp :tangle no
    (require 'evil-org-agenda)
    (evil-org-agenda-set-keys)
  #+end_src


  #+begin_src emacs-lisp
    (use-package evil-org
      :after org
      :hook
      (org-mode . evil-org-mode)
      (evil-org-mode . (lambda ()
                         (evil-org-set-key-theme))))
  #+end_src

* restclient
  #+begin_src emacs-lisp
    (use-package restclient
      :defer t)

    (use-package restclient-jq
      :defer t)

    (use-package ob-restclient
      :defer t
      :after (org restclient)
      :init (add-to-list 'org-babel-load-languages '(restclient . t)))
  #+end_src

* org password manager

  #+begin_src emacs-lisp
    (defvar pair-list nil) ;; a property list
    (defvar secret-file-path "~/Dropbox/myorgs/management/learning.org.gpg")

    (defun iterate-org-level (&optional input)
      (interactive)
      ;; we need to escape the space in the property
      ;; ex. (setq a '(:abc\ cde 1))
      (require 'epa-file)
      (require 'org-element)
      (with-temp-buffer
        (epa-file-insert-file-contents secret-file-path)
        (setq pair-list nil)
        (cl-loop for i from 0
                 for ele in (org-element-parse-buffer 'headline)
                 when (and (> i 0) (not (equal ele nil)))
                 do (let* ((prop (plist-get ele 'headline))
                           (domain (plist-get prop :DOMAIN))
                           (title (plist-get prop :title))
                           (pass (plist-get prop :SECRET)))

                      (setq pair-list (plist-put pair-list (intern (format ":%s--%s" title domain)) pass))))
        (cl-loop for i from 0 for ele in pair-list
                 when (cl-evenp i) collect (symbol-name ele))))


    (defun get-se-action (x)
      (kill-new
       (base64-decode-string
        (decode-coding-string
         (plist-get pair-list (intern x)) 'utf-8)))
      (message "copy secret into clipboard"))

    (defun get-secret ()
      (interactive)
      (cond
       ((featurep 'ivy)
        (ivy-read
         "choose: " (iterate-org-level)
         :action #'get-se-action
         :caller 'get-secret))
       ((featurep 'consult)
        (let ((candidates (iterate-org-level)))
          (get-se-action
           (consult--read
            candidates
            :prompt "choose: "
            :sort nil
            :preview-key nil
            :require-match t))))))




    (defun random-bytes (n)
      "Return a unibyte string of N random bytes."
      (if (file-readable-p "/dev/urandom")
          ;; Use system CSPRNG if available
          (with-temp-buffer
            (set-buffer-multibyte nil)
            (insert-file-contents-literally "/dev/urandom" nil nil n)
            (buffer-string))
        ;; Fallback to Emacs PRNG
        (let ((s (make-string n 0)))
          (dotimes (i n s)
            (aset s i (random 256))))))

    (defun secure-random-password (&optional length algo symbols)
      "Generate a password of LENGTH chars using ALGO hash and SYMBOLS charset.
    Defaults: LENGTH=20, ALGO='sha256, SYMBOLS=alnum+symbols."
      (let* ((len (or length 20))
             (charset (concat "abcdefghijklmnopqrstuvwxyz"
                              "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
                              "0123456789"
                              (or symbols "!@#$%^&*_-+=")))
             (clen (length charset))
             ;; Step 1: get random bytes
             (seed (random-bytes 64))
             ;; Step 2: hash them with secure-hash
             (digest (secure-hash (or algo 'sha256) seed nil nil t))
             ;; Step 3: repeat hash if we need more material
             (pool (apply #'concat
                          (cl-loop repeat 4
                                   collect (secure-hash (or algo 'sha256)
                                                        (concat digest seed)
                                                        nil nil t)))))
        ;; Step 4: map hash bytes into charset
        (apply #'string
               (cl-loop for i below len
                        for b = (aref pool i)
                        collect (aref charset (% b clen))))))

    (defun insert-secure-random-password (length)
      "Prompt for LENGTH and insert a secure random password at point."
      (interactive "nPassword length: ")
      (insert (secure-random-password length)))

  #+end_src

* org copy subtree's content

  #+begin_src emacs-lisp
    (defun org-copy-subtree-content (arg)
      "copy subtree content without header"
      (interactive "P")
      ;; but with this, it can resolve that problem.
      (save-excursion
        (org-back-to-heading)
        (forward-line)
        (let ((beg (point))
              (end (point)))
          (outline-end-of-subtree)
          (setq end (point))
          (if arg
              (kill-new (buffer-substring-no-properties beg end))
            (org-copy-visible beg end))))

      ;; it's weird sometime the vertico minibuffer will be stucked
      (message "copy content without header"))
  #+end_src

* insert a time section in routine

  #+begin_src emacs-lisp
    (defun insert-new-time-section-under-routine ()
      "Insert a new subheader under the main header in Org mode."
      (interactive)
      (save-excursion
        (goto-char (point-min))
        (if (re-search-forward "^\\* routines$" nil t)
            (if (re-search-forward (concat "^\\*\\{2,5\\} " (format-time-string "%H:00:00")) nil t)
                (org-show-subtree)
              (progn
                (org-end-of-subtree)
                (if (= (org-outline-level) 1)
                    (call-interactively 'org-insert-subheading nil)
                  (org-insert-heading))
                (insert (format-time-string "%H:00:00"))))

          (message "Hint: The word \"* routines\" is not found. You should call this in the daily routine."))))
  #+end_src

* org-insert-toc
  FUTURE: maybe we can enhance this with prompting like org-insert-link
  #+begin_src emacs-lisp
    (defun org-insert-toc ()
      "Insert table of content for org mode."
      (interactive)
      (beginning-of-line)
      (insert "*" " " ":TOC:")
      (backward-char 5)
      (evil-insert-state))
  #+end_src

* org table configuration

  #+begin_src emacs-lisp
    (with-eval-after-load 'org
      (defcustom org-html-tableel-org "no"
        "Export table.el cells as org code if set to \"t\" or \"yes\".
    This is the default and can be changed per section with export option:
    ,#+OPTIONS: HTML_TABLEEL_ORG: t"
        :type '(choice (const "no") (const "yes"))
        :group 'org-html)

      (eval-after-load 'ox-html
        '(eval ;;< Avoid eager macro expansion before ox-html is loaded.
          '(cl-pushnew
            (list
             :html-tableel-org
             "HTML_TABLEEL_ORG" ;; keyword
             "HTML_TABLEEL_ORG" ;; option for #+OPTIONS: line
             org-html-tableel-org ;; default value for the property
             t ;; handling of multiple keywords for the same property. (Replace old value with new one.)
             )
            (org-export-backend-options (org-export-get-backend 'html)))))

      (defvar org-element-all-elements) ;; defined in "org-element"
      (defun table-generate-orghtml-cell-contents (dest-buffer language cell info)
        "Generate and insert source cell contents of a CELL into DEST-BUFFER.
    LANGUAGE must be 'orghtml."
        (cl-assert (eq language 'html) nil
                   "Table cells with org content only working with html export")
        (let* ((cell-contents (extract-rectangle (car cell) (cdr cell)))
               (string (with-temp-buffer
                         (table--insert-rectangle cell-contents)
                         (table--remove-cell-properties (point-min) (point-max))
                         (goto-char (point-min))
                         (buffer-substring (point-min) (point-max)))))
          (with-current-buffer dest-buffer
            (let ((beg (point)))
              (insert (org-export-string-as string 'html t info))
              (indent-rigidly beg (point) 6)))))

      (defun my-org-html-table--table.el-table (table _info)
        "Format table.el tables into HTML.
    INFO is a plist used as a communication channel."
        (when (eq (org-element-property :type table) 'table.el)
          (require 'table)
          (let ((outbuf (with-current-buffer
                            (get-buffer-create "*org-export-table*")
                          (erase-buffer) (current-buffer))))
            (with-temp-buffer
              (insert (org-element-property :value table))
              (goto-char 1)
              (re-search-forward "^[ \t]*|[^|]" nil t)
              (table-recognize-region (point-min) (point-max) 1)
              (table-generate-source 'html outbuf))
            (with-current-buffer outbuf
              (prog1 (org-trim (buffer-string))
                (kill-buffer))))))

      (defun org-orghtml-table--table.el-table (fun table info)
        "Format table.el TABLE into HTML.
    This is an advice for `org-html-table--table.el-table' as FUN.
    INFO is a plist used as a communication channel."
        (if (assoc-string (plist-get info :html-tableel-org) '("t" "yes"))
            (cl-letf (((symbol-function 'table--generate-source-cell-contents)
                       (lambda (dest-buffer language cell)
                         (table-generate-orghtml-cell-contents dest-buffer language cell info))))
              (funcall fun table info))
          (funcall fun table info)))

      (advice-add 'org-html-table--table.el-table :override #'my-org-html-table--table.el-table)
      (advice-add #'my-org-html-table--table.el-table :around #'org-orghtml-table--table.el-table))
  #+end_src

* org configuration

  example settings for org-agenda-files
  #+begin_example
  (setq org-agenda-files (file-expand-wildcards "~/Dropbox/myorgs/*.org"))
  (setq org-agenda-files (directory-files-recursively "~/Dropbox/myorgs/" "\\.org$"))
  #+end_example


  #+begin_src emacs-lisp
    (with-eval-after-load 'org
      (org-babel-do-load-languages
       'org-babel-load-languages
       '((emacs-lisp . t)
         (shell . t)
         (dot . t)
         (sql . t)
         (lua . t)
         (js . t)
         (python . t)))

      ;; NOTE: we need to add this to make org src block highlight the syntax
      (add-to-list 'org-src-lang-modes '("python" . python))

      (add-to-list 'org-structure-template-alist '("sel" . "src emacs-lisp"))
      (add-to-list 'org-structure-template-alist '("sb" . "src bash"))
      (add-to-list 'org-structure-template-alist '("sp" . "src python"))


      (set-face-attribute 'org-block nil :background "#202021")
      (set-face-attribute 'org-quote nil :background "#202021")


      ;; set org table's font
      ;; (set-face-font 'org-table " ")
      ;; I use the visual-column instead
      ;; (add-hook 'org-mode-hook 'toggle-word-wrap)

      ;; Set faces for heading levels
      (dolist (face '((org-document-title . 1.5)
                      (org-level-1 . 1.3)
                      (org-level-2 . 1.2)
                      (org-level-3 . 1.15)
                      (org-level-4 . 1.1)
                      (org-level-5 . 1.0)
                      (org-level-6 . 1.0)
                      (org-level-7 . 1.0)
                      (org-level-8 . 1.0)))
        (set-face-attribute (car face) nil :font "Source Code Pro" :weight 'regular :height (cdr face)))

      ;; NOTE:
      ;; (setq org-format-latex-options
      ;;        (list :foreground 'default
      ;;              :background 'default
      ;;              :scale 1.5
      ;;              :html-foreground "Black"
      ;;              :html-background "Transparent"
      ;;              :html-scale 1.0
      ;;              :matchers '("begin" "$1" "$" "$$" "\\(" "\\[")))

      (setq org-journal-dir "~/Dropbox/myorgs/journal/")
      (setq org-journal-file-type 'weekly)
      (setq org-journal-file-format "%Y-%m-%W.org")

      (setq org-agenda-files (split-string (shell-command-to-string "find ~/Dropbox/myorgs -type f | grep '.*.org$' | grep -E -v 'presentation/|journal/'") "\n" t))

      ;; to config the org refile
      (setq org-refile-targets '((org-agenda-files :maxlevel . 3)))
      (setq org-refile-use-outline-path 'file)
      (setq org-outline-path-complete-in-steps nil)

      ;; to allow creating a new heading when performing the org refile
      (setq org-refile-allow-creating-parent-nodes 'confirm)
      (setq org-superstar-headline-bullets-list '("❐" "○" "✎" "⚈"))


      (setq org-todo-keywords
            '((sequence "TODO" "IN PROGRESS(i!)" "|" "DONE" "PRESERVE")))

      (setq org-todo-keyword-faces
            '(("TODO" . "#dc752f")
              ("IN PROGRESS" . "#33eecc")
              ("NO_NEWS" . "#cdb7b5")
              ("ABANDON" . "#f2241f")
              ("OFFERGET" . "#4f97d7"))))
  #+end_src

* provide package

  #+begin_src emacs-lisp
    (provide 'jemacs-note-taking)
    ;;; jemacs-note-taking.el ends here
  #+end_src
